
attribute vec2 aVertexPosition;

uniform mat3 projectionMatrix;

varying vec2 v_rgbNW;
varying vec2 v_rgbNE;
varying vec2 v_rgbSW;
varying vec2 v_rgbSE;
varying vec2 v_rgbM;

uniform vec4 destinationFrame;
uniform vec4 sourceFrame;

varying vec2 vTextureCoord;

vec4 filterVertexPosition( void )
{
    vec2 position = aVertexPosition * max(sourceFrame.ba, vec2(0.)) + sourceFrame.xy;

    return vec4((projectionMatrix * vec3(position, 1.0)).xy, 0.0, 1.0);
}

vec2 filterTextureCoord( void )
{
    return aVertexPosition * (sourceFrame.ba / destinationFrame.ba);
}

void texcoords(vec2 fragCoord, vec2 resolution,
               out vec2 v_rgbNW, out vec2 v_rgbNE,
               out vec2 v_rgbSW, out vec2 v_rgbSE,
               out vec2 v_rgbM) {
    vec2 inverseVP = 1.0 / resolution.xy;
    v_rgbNW = (fragCoord + vec2(-1.0, -1.0)) * inverseVP;
    v_rgbNE = (fragCoord + vec2(1.0, -1.0)) * inverseVP;
    v_rgbSW = (fragCoord + vec2(-1.0, 1.0)) * inverseVP;
    v_rgbSE = (fragCoord + vec2(1.0, 1.0)) * inverseVP;
    v_rgbM = vec2(fragCoord * inverseVP);
}

void main(void) {

   gl_Position = filterVertexPosition();

   vTextureCoord = filterTextureCoord();

   vec2 fragCoord = vTextureCoord * destinationFrame.ba;

   texcoords(fragCoord, destinationFrame.ba, v_rgbNW, v_rgbNE, v_rgbSW, v_rgbSE, v_rgbM);
}